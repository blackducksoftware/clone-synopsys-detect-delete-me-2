import com.synopsys.integration.detect.docs.GenerateDocsTask

task helpJson(type: JavaExec) {
    dependsOn build

    classpath = files(createArtifactName())
    classpath += sourceSets.main.runtimeClasspath
    main = 'com.synopsys.integration.detect.Application'
    args = ['--helpjson']
}

task generateDocs(type: GenerateDocsTask) {
    dependsOn helpJson
}

task docs() {
    dependsOn generateDocs

    doLast {
        exec {
            commandLine 'mkdocs', 'build'
            workingDir "docs"
        }
    }
}

task ditazip(type: Zip) {
    dependsOn generateDocs

    archiveName "${project.name}-${version}-ditasource.zip"
    from 'docs/generated'
    destinationDir(file("${buildDir}/libs/"))
}

task ditasite() {
    dependsOn generateDocs

    doLast {
        exec {
            commandLine 'dita', '--input=ditamap.xml', '--format=sig-webhelp', '-o', '../ditasite'
            workingDir "docs/generated"
        }
    }
}

task helpzip(type: Zip) {
    dependsOn generateDocs

    archiveName "${project.name}-${version}-help.zip"
    from 'docs/ditasite'
    destinationDir(file("${buildDir}/libs/"))
}

task ditadocs() {
    dependsOn ditasite
    dependsOn ditazip
}

task servedocs() {
    dependsOn docs

    doLast {
        exec {
            commandLine 'mkdocs', 'serve'
            workingDir "docs"
        }
    }
}
