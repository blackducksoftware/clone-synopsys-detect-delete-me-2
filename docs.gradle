import com.synopsys.integration.detect.docs.GenerateDocsTask

task helpJson(type: JavaExec) {
    dependsOn build

    classpath = files(createArtifactName())
    classpath += sourceSets.main.runtimeClasspath
    main = 'com.synopsys.integration.detect.Application'
    args = ['--helpjson']
}

task generateDocs(type: GenerateDocsTask) {
    dependsOn helpJson
}

task docs() {
    dependsOn generateDocs

    doLast {
        exec {
            commandLine 'mkdocs', 'build'
            workingDir "docs"
        }
    }
}

task ditasourcezip(type: Zip) {
    dependsOn generateDocs

    archiveName "${project.name}-${version}-ditasource.zip"
    from 'docs/generated'
    destinationDir(file("${buildDir}/libs/"))
}

task publishDitaSource() {
    dependsOn ditasourcezip

    doLast {
        def ditaSourceZipName = "${project.name}-${version}-ditasource.zip"
        def ditaSourceZipLocation = "${buildDir}/libs/${ditaSourceZipName}"
        exec {
            commandLine 'curl', '--insecure', '-u', "${project.ext.artifactoryDeployerUsername}:${project.ext.artifactoryDeployerPassword}", '-X', 'PUT',
                    "${project.ext.deployArtifactoryUrl}/${project.ext.artifactoryRepo}/com/synopsys/integration/${project.name}/${version}/${ditaSourceZipName}", '-T', "${ditaSourceZipLocation}", '-f'
        }
    }
}

// Requires Synopsys-configured dita:
// 1. git clone https://sig-gitlab.internal.synopsys.com/doc/docs-dita-tools.git
// 2. Add docs-dita-tools/dita-ot/bin to your PATH
task ditasite() {
    dependsOn generateDocs

    doLast {
        ByteArrayOutputStream stdOut = new ByteArrayOutputStream()
        exec {
            commandLine 'dita', '-v', '--input=detect.ditamap', '--format=sig-webhelp', '-o', '../ditasite'
            workingDir "docs/generated"
            standardOutput = stdOut
        }
	for (String outputLine: stdOut.toString().split('\n')) {
	    if (outputLine.matches(".*\\[keyref\\].*\\[DOTJ047I\\].*"))
		throw new Exception("dita found invalid key references. Square brackets around doc text must be escaped if not inside back quotes.")
	}
    }
}

task helppdf() {
    dependsOn generateDocs

    doLast {
        exec {
            commandLine 'dita', '--input=detect.ditamap', '--format=sig-pdf', "--outputFile.base=${project.name}-${version}-help", '-o', '../../build/libs'
            workingDir "docs/generated"
        }
    }
}

task helpzip(type: Zip) {
    dependsOn ditasite

    archiveName "${project.name}-${version}-help.zip"
    from 'docs/ditasite'
    destinationDir(file("${buildDir}/libs/"))
}

task servedocs() {
    dependsOn docs

    doLast {
        exec {
            commandLine 'mkdocs', 'serve'
            workingDir "docs"
        }
    }
}
